; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Ancestris"
#define MyAppVersion "11.0"
#define MyAppPublisher "Ancestris Team"
#define MyAppURL "https://www.ancestris.org"
#define MyAppExeName "ancestris.exe"
#define MyIcon "ancestris.ico"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{593EAF75-04C7-4807-98FB-E3EEAD229E53}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
; The [Icons] "quicklaunchicon" entry uses {userappdata} but its [Tasks] entry has a proper IsAdminInstallMode Check.
UsedUserAreasWarning=no
LicenseFile=D:\ancestris\sources\Tools\trancestris\gpl-2.0.txt
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
OutputDir=D:\ancestris\ancestris_dist\inno
OutputBaseFilename=AncestrisSetup
Compression=lzma
SolidCompression=yes
WizardStyle=modern
WizardImageFile=D:\ancestris\ancestris_dist\inno\Logo.bmp
WizardSmallImageFile=D:\ancestris\ancestris_dist\inno\ancestris128.bmp
ArchitecturesInstallIn64BitMode=x64 ia64 arm64
DisableWelcomePage=false

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "brazilianportuguese"; MessagesFile: "compiler:Languages\BrazilianPortuguese.isl"
Name: "catalan"; MessagesFile: "compiler:Languages\Catalan.isl"
Name: "czech"; MessagesFile: "compiler:Languages\Czech.isl"
Name: "danish"; MessagesFile: "compiler:Languages\Danish.isl"
Name: "dutch"; MessagesFile: "compiler:Languages\Dutch.isl"
Name: "french"; MessagesFile: "compiler:Languages\French.isl"
Name: "german"; MessagesFile: "compiler:Languages\German.isl"
Name: "italian"; MessagesFile: "compiler:Languages\Italian.isl"
Name: "norwegian"; MessagesFile: "compiler:Languages\Norwegian.isl"
Name: "polish"; MessagesFile: "compiler:Languages\Polish.isl"
Name: "portuguese"; MessagesFile: "compiler:Languages\Portuguese.isl"
Name: "spanish"; MessagesFile: "compiler:Languages\Spanish.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; OnlyBelowVersion: 6.1; Check: not IsAdminInstallMode

[Files]
Source: "D:\ancestris\ancestris_dist\ancestris_11-20210529\ancestris\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "D:\ancestris\ancestris_dist\inno\jre8-32\*"; DestDir: "{app}\jre8"; Flags: ignoreversion recursesubdirs createallsubdirs; Check: (not Is64BitInstallMode) and getJavaToInstall
Source: "D:\ancestris\ancestris_dist\inno\jre8-64\*"; DestDir: "{app}\jre8"; Flags: ignoreversion recursesubdirs createallsubdirs; Check: (Is64BitInstallMode) and getJavaToInstall
Source: "D:\ancestris\ancestris_dist\inno\ancestris.conf"; DestDir: "{app}\etc"; Flags: ignoreversion; Check: getJavaToInstall
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\bin\{#MyAppExeName}"; WorkingDir: "{app}\bin"; IconFilename: "{app}\bin\{#MyIcon}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\bin\{#MyAppExeName}"; Tasks: desktopicon; IconFilename: "{app}\bin\{#MyIcon}"
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}"; Filename: "{app}\bin\{#MyAppExeName}"; Tasks: quicklaunchicon; IconFilename: "{app}\bin\{#MyIcon}"

[Run]
Filename: "{app}\bin\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[Code]
var
  javaToInstall : boolean;

function getJavaToInstall() : boolean;
begin
    Result := javaToInstall;
end;

function GetJavaVersion() : string;
var
  TempFile: string;
  ResultCode: Integer;
  S: AnsiString;
  P: Integer;
begin
  TempFile := ExpandConstant('{tmp}\javaversion.txt');
  if (not ExecAsOriginalUser(
            ExpandConstant('{cmd}'), '/c java -version 2> "' + TempFile + '"', '',
            SW_HIDE, ewWaitUntilTerminated, ResultCode)) or
     (ResultCode <> 0) then
  begin
    Log('Failed to execute java -version');
  end
    else
  if not LoadStringFromFile(TempFile, S) then
  begin
    Log(Format('Error reading file %s', [TempFile]));
  end
    else
  if Copy(S, 1, 14) <> 'java version "' then
  begin
    Log('Output of the java -version not as expected');
  end
    else
  begin
    Delete(S, 1, 14);
    P := Pos('"', S);
    if P = 0 then
    begin
      Log('Output of the java -version not as expected');
    end
      else
    begin
      SetLength(S, 5);
      Result := S;
    end;
  end;

  DeleteFile(TempFile);
end;

function HasJava1Dot8OrNewer(): Boolean;
var
  retour : boolean;
begin
  retour := (GetJavaVersion < '1.8.0');
  Result := retour;
end;

procedure InitializeWizard();
begin
    javaToInstall :=  HasJava1Dot8OrNewer();
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
 if (CurStep = ssPostInstall) and (javaToInstall = True) then
  begin
    SaveStringToFile(
      ExpandConstant('{app}\etc\ancestris.conf'),
      'jdkhome="' + ExpandConstant('{app}\jre8') +'"', True);
  end;

end;
